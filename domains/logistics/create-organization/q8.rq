prefix ex: <http://example.org/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix hmas: <https://purl.org/hmas/>

select ?orgGoal
where {
  ex:SL_Logistics a ?orgModel .
  ?orgModel rdfs:subClassOf hmas:Organization ;
            sh:property [
              sh:path  hmas:hasOrganizationalGoal ;
              sh:qualifiedValueShape [ sh:class ?orgGoalModel ] ;
            ] .
  { ?orgModel a sh:NodeShape . }
  union
  { ?orgShape a sh:NodeShape ;
              sh:targetClass ?orgModel .
  }

  ?orgGoalModel rdfs:subClassOf hmas:OrganizationalGoal .
  { ?orgGoalModel a sh:NodeShape . }
  union
  { ?orgGoalShape a sh:NodeShape ;
                  sh:targetClass ?orgGoalModel .
  }
  
  ?orgGoal a ?orgGoalModel
}
order by ?orgGoal
