prefix ex: <http://example.org/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix hmas: <https://purl.org/hmas/>

select distinct ?agent
where {
  ex:Jane hmas:isMemberOf ex:FL_Logistics .
  
  ex:FL_Logistics a ?orgModel ;
                  hmas:providesRole ?role ;
                  hmas:isStructuredBy ?group .
  
  ?orgModel rdfs:subClassOf hmas:Organization .
  { ?orgModel a sh:NodeShape . }
  union
  { ?orgShape a sh:NodeShape ;
              sh:targetClass ?orgModel . }
  
  ?membershipModel rdfs:subClassOf hmas:Membership .
  { ?membershipModel a sh:NodeShape . }
  union
  { ?membershipShape a sh:NodeShape ;
                     sh:targetClass ?membershipModel .
  }

  ?membership a ?membershipModel ;
              hmas:isMembershipOf ex:Jane ;
              hmas:isMembershipFor ?role ;
              hmas:isMembershipIn ?group .

  ?interaction rdfs:subPropertyOf hmas:hasMembershipInteractionWith .

  ?membership ?interaction ?otherMembership .

  ?otherMembership a ?otherMembershipModel ;
                   hmas:isMembershipOf ?agent .
}
order by ?agent
